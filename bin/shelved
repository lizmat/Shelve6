#!/usr/bin/env raku

use Shelved;

my $log := Shelved::Logging.new('main');

sub MAIN(Str :$config-file = 'config.yaml') {
    $log.info("Shelved starting...");
    $log.debug("Reading configuration from $config-file...");
    # XXX validate top level keys and that the top is a hash, and that repositories
    # is a list
    my %config = load-config($config-file);

    my $store  := Shelved::Store.new(|%config<store>);
    my $server := Shelved::Server.new(|%config<server>);
    my @repositories;
    for @(%config<repositories>) -> $repo {
        my $repo-object := Shelved::Repository.new(:$server, :$store, |%($repo));
        push @repositories, $repo-object;
    }

    $log.debug("Starting components...");
    $store.start;
    for @repositories { .start }
    $server.start;

    $log.info("Application initialization complete!");

    react whenever signal(SIGINT) {
        $log.debug("Shutting down components...");
        $server.stop;
        for @repositories { .stop }
        $store.stop;
        $log.info("Clean shutdown, see you later!");
        exit;
    }
}
